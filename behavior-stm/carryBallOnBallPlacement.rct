import utils

interface NodeStatusI {
	var nodeStatus : NODE_STATUS
}

interface CarryBallI{
	carryBall(frame: Frame, targetPosition: Point, distanceThreshold: real)
}

interface GoToBehindBallI {
	goToBehindBall(targetPosition: Point, angleThreshold: real)
}

interface ApproachBallI{
	approachBall(ball: Ball)
}

interface MoveAwayFromPositionI {
	moveAwayFromPosition(frame: Frame, dangerousPosition: Point, distanceThreshold: real)
}

enumeration NODE_STATUS {
	RUNNING
	FAILED
	DONE
}

operation carryBallOnBallPlacement(frame: Frame, targetPosition: Point) {
	requires CarryBallI
	requires GoToBehindBallI
	requires ApproachBallI
	requires MoveAwayFromPositionI
	requires NodeStatusI
	
	// Robot instance.
	var robot: Robot
	var ball: Ball = getBall(frame)
	
	// Constants.
	const DIST_THRESHOLD_BALL_TO_TARGET: real = 150
	const DIST_THREHOLD_ROBOT_TO_TARGET_ON_FINISH: real = 500
	const MIN_ANGLE_DIFF_TO_CONSIDER_ALIGNED_WITH_BALL_IN_DEGREES: real = 60
		
	// States.
	initial Initial
	state GoToBehindBall {
		entry nodeStatus = NODE_STATUS::RUNNING; goToBehindBall(targetPosition, MIN_ANGLE_DIFF_TO_CONSIDER_ALIGNED_WITH_BALL_IN_DEGREES)
	}
	state ApproachBall {
		entry nodeStatus = NODE_STATUS::RUNNING; approachBall(ball)
	}
	state PlaceBall {
		entry nodeStatus = NODE_STATUS::RUNNING; carryBall(frame, targetPosition, DIST_THRESHOLD_BALL_TO_TARGET)
	}
	state MoveAwayFromBall {
		entry nodeStatus = NODE_STATUS::RUNNING; moveAwayFromPosition(frame, targetPosition, DIST_THREHOLD_ROBOT_TO_TARGET_ON_FINISH)
	}
	final Final
	
	// Transitions.
	transition AdjustToCarryBall {
		from Initial
		to GoToBehindBall
	}
	
	transition GetBallControl {		
		from GoToBehindBall
		to ApproachBall
		//condition (allyIsBehind(robot, targetPosition, MIN_ANGLE_DIFF_TO_CONSIDER_ALIGNED_WITH_BALL_IN_DEGREES) /\ allyIsCloseToBall(robot, ball))
		condition nodeStatus == NODE_STATUS::DONE
	}
	
	transition PlaceBallInPosition {
		from ApproachBall
		to PlaceBall
		//condition allyHasBall(robot, ball)
		condition nodeStatus == NODE_STATUS::DONE
	}
	
	transition MoveAway {
		from PlaceBall	
		to MoveAwayFromBall
		//condition getDistance(getBallPosition(frame), targetPosition) < DIST_THRESHOLD_BALL_TO_TARGET
		condition nodeStatus == NODE_STATUS::DONE
	}
	
	transition Finish {
		from MoveAwayFromBall
		to Final
		//condition getDistance(getRobotPosition(frame), targetPosition) < DIST_THREHOLD_ROBOT_TO_TARGET_ON_FINISH
		condition nodeStatus == NODE_STATUS::DONE
	}
}	

